\section{Fuzzing with Echidna}
Fuzzing refers to the process of feeding random input data to a program in order to potentially induce undesirable behavior. Sometimes the inputs are adaptively chosen in order to maximize coverage. The existence of unexpected behavior indicates the presence of a bug---potentially an exploitable vulnerability. Echidna~\cite{echidna} is a fuzzing tool for Ethereum smart contracts that checks for the violation of assertions or user defined properties (indicated using a special prefix). It simulates random calls to various public functions in the contract from a partially configurable set of senders. The user may specify which functions to call and can also monitor the maximum gas usage by each function. In case a property is violated, a sequence of calls leading to the violation is outputted (Echidna tries to shorten the violating sequence if possible). Echidna can thus detect several kind of elementary bugs such as ones resulting from race conditions, integer overflows, unintended self destruct, or uncontrolled gas usage. For instance, consider the following (somewhat contrived) contract that attempts to create a bank that allows users to specify a minimum balance they want to maintain. However, there is a bug in the balance check for withdrawal that allows a user induce an integer overflow and thus withdraw around $2^{64}$ wei while maintaining minimal balance. To test this, we add a property to check for inconsistent total deposit and withdrawal amounts as shown below. We also comment out the checks for values and transfers to enable us to use default settings for Echidna (this step can be eliminated by tweaking the configuration).
\begin{lstlisting}[basicstyle=\small]
contract OverflowBank {
  mapping(address => uint64) public balance;  
  mapping(address => uint64) public minBalance;   
  uint64 public maxDeposit = 1e18;
  uint64 public maxBalance = 1e18; 
  uint256 public totalDeposit = 0;
  uint256 public totalWithdrawal = 0;

  function setMinBalance(uint64 newMinBalance) 
  public {
    require(newMinBalance 
            <= balance[msg.sender]);
    minBalance[msg.sender] = newMinBalance;
  }

  function deposit(uint64 amount) 
  public payable {
    //require(amount <= msg.value);
    require(amount <= maxBalance);
    require(amount + balance[msg.sender] 
            <= maxBalance);
    balance[msg.sender] += amount;
    totalDeposit += uint256(amount);
  }

  function withdraw(uint64 amount) public {
    // Overflow bug
    require(balance[msg.sender] 
            >= minBalance[msg.sender] + amount); 
    balance[msg.sender] -= amount;
    totalWithdrawal += uint256(amount);
    //msg.sender.transfer(amount);
  }

  function echidna_check_loss() 
  public returns (bool) {
    // No money is lost
    return(totalWithdrawal <= totalDeposit); 
  }
}
\end{lstlisting}
In this case, Echidna returns the call sequence 
\begin{lstlisting}[basicstyle=\small]
deposit(103170786233548238)
setMinBalance(102246380585095486)
withdraw(18345214573411047223)
\end{lstlisting}
that does indeed induce the overflow. However, fuzzing alone is ineffective against more subtle issues like re-entrancy which might only be detectable if fuzzing is integrated with runtime monitoring and/or static analysis as some other tools attempt to do. Moreover, a lot of bugs only manifest in rare edge cases, which are unlikely to be detected using random inputs.

However, since Echidna is primarily intended as a tool for testing contracts, it requires a basic understanding of the code in order to be utilized effectively. Few contracts include assertions in the source and thus suitable properties need to be devised and implemented. Although some generic techniques such as checking ranges of balances and measuring gas consumption usually apply, they alone are likely to catch a vanishingly small number of bugs. Hence, the attack needs to be tailored for each contract thereby making Echidna unsuitable for large-scale vulnerability detection. Therefore, for our purposes, we use the static analysis tool Slither~\cite{slither} to identify contracts which might have detectable issues based on an analysis of source code in order to narrow down the search. Note that Slither is much more susceptible to false positives as compared to Echidna as the latter produces an actual sequence of calls in which the issue materializes. On the other hand, this narrowing down significantly increases the rate of false negatives. However, resource limitations compel us to follow this strategy.

Slither only works on versions 0.4.x of Solidity greatly limiting the number of contracts that can be scanned. Among contracts with non-zero balance, Slither identified only a handful with potential bugs that could be detectable using Echidna. Running Echidna on these as well as a few randomly picked contracts revealed no vulnerabilities despite several hours of effort. No critical invariants were found to be broken and no functions took up unusual amounts of gas. However, Slither alone is capable of detecting a much larger class of errors such as re-entrancy. For instance, Slither could have detected the vulnerability that led to the DAO hack~\cite{slither}. This raises questions as to the practicality of using fuzzers since they effectively only rule out false positives.